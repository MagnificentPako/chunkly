📜🔤queue.🍇🔤
📜🔤nbt_types.🍇🔤

🐇 🆔 🍇
    🖍🆕 type 💧
    🖍🆕 name 🔡

    🆕 🍼 type 💧 🍼 name 🔡 🍇🍉

    ❓💧➡️💧🍇 ↩️type 🍉
    ❓📛➡️🔡🍇 ↩️name 🍉

    ❓🔡➡️🔡 🍇
        🆕🔠🆕❗️ ➡️ builder
        🐻builder 📛🐇🇳🔸🅱🔸️🇹 🔢type❗️ ❗️❗️
        🐻builder 🔤('🔤❗️
        🐻builder name❗️
        🐻builder 🔤')🔤❗️
        ↩️🔡builder❗️
    🍉
🍉

🐇 🇳🔸🅱🔸️🇹 🍇

    🖍🆕 queue 🐍
    🖍🆕 root 😩
    
    🆕 raw_data 📇 🍇
        🆕🐍🆕 raw_data❗️ ➡️ 🖍queue
        🆕😩🔸🏁🆕❗️ ➡️ 🖍root

        😩🐕❗️ ➡️ 🖍root

        😀🔡root❓❗️
    🍉

    ❗️🆔➡️🆔🍇
        💧🎈queue❗️❗️ ➡️ type
        👶queue❗️ ➡️ name_length
        🆕🔠🆕❗️    ➡️ name_builder
        ↪️ name_length ️▶️ 0 🍇
            🐻name_builder 🔡queue name_length❗️❗️
        🍉

        ↩️🆕🆔🆕 type 🔡name_builder❗️❗️
    🍉

    ❗️😩➡️😩🍇
        🆔🐕❗️ ➡️ tag
        🔢💧tag❓❗️ ➡️ id

        😩🔸😩🐕 tag❗️ ➡️ elem
        ↩️elem
    🍉

    ❗️😩🔸😩 tag 🆔 ➡️😩🍇
        🔢💧tag❓❗️ ➡️ id

        💭Byte
        ↪️ id 🙌 1 🍇
            🎈queue❗️ ➡️ byte
            ↩️🆕😩🔸💧🆕 📛tag❓ 💧byte❗️❗️
        🍉

        💭Short
        🙅↪️ id 🙌 2 🍇
            👶queue❗️ ➡️ short
            ↩️🆕😩🔸🐈🆕 📛tag❓ short❗️
        🍉

        💭Int
        🙅↪️ id 🙌 3 🍇
            🔢queue❗️ ➡️ int
            ↩️🆕😩🔸🐈🆕 📛tag❓ int❗️
        🍉

        💭Long
        🙅↪️ id 🙌 4 🍇
            🐈queue❗️ ➡️ long
            ↩️🆕😩🔸🐈🆕 📛tag❓ long❗️
        🍉

        💭Float
        🙅↪️ id 🙌 5 🍇
            💯queue❗️ ➡️ float
            ↩️🆕😩🔸💯🆕 📛tag❓ float❗️
        🍉

        💭double
        🙅↪️ id 🙌 6 🍇
            🔂 i 🆕⏩⏩ 0 8❗️ 🍇
                🎈queue❗️
            🍉
            ↩️🆕😩🔸💯🆕 📛tag❓ 0❗️
        🍉

        💭Byte Array
        🙅↪️ id 🙌 7 🍇
            🔢queue❗️ ➡️ length

            🆕🍨🐚🔢🍆🐸❗️ ➡️ 🖍🆕bytes
            
            🔂 i 🆕⏩⏩ 0 length❗️ 🍇
                🐻bytes 🎈queue❗️❗️
            🍉

            ↩️🆕😩🔸🍨🔸🔢🆕 📛tag❓ bytes❗️
        🍉

        💭String
        🙅↪️ id 🙌 8 🍇
            👶queue❗️ ➡️ str_len
            🔡queue str_len❗️ ➡️ str
            ↩️🆕😩🔸🔡🆕 📛tag❓ str❗️
        🍉

        💭List
        🙅↪️ id 🙌 9 🍇
            🎈queue❗️ ➡️ type
            🔢queue❗️ ➡️ length
            🆕🍨🐚😩🍆🐸❗️ ➡️ 🖍🆕elements
            
            ↪️length ▶ 0 🍇
                🔂 i 🆕⏩⏩ 0 length❗️ 🍇
                    🐻elements 😩🔸😩🐕 🆕🆔🆕 💧type❗️ 🔤🔤 ❗️❗️❗️
                🍉
            🍉

            ↩️🆕😩🔸🍨🆕 📛tag❓ elements❗️
        🍉

        💭Compound
        🙅↪️ id 🙌 10 🍇
            🆕🍨🐚😩🍆🐸❗️ ➡️ 🖍🆕compound_content

            🔁❎ 🔢👁queue❓❗️ 🙌 0❗️🍇
                🐻compound_content 😩🐕❗️❗️
            🍉
            🎈queue❗️
            ↩️🆕😩🔸📁🆕 📛tag❓ compound_content❗️ ️
        🍉

        💭Int Array
        🙅↪️ id 🙌 11 🍇
            🔢queue❗️ ➡️ length
            🆕🍨🐚🔢🍆🐸❗️ ➡️ 🖍🆕ints
            
            🔂 i 🆕⏩⏩ 0 length❗️ 🍇
                🐻ints 🔢queue❗️❗️
            🍉

            ↩️🆕😩🔸🍨🔸🔢🆕 📛tag❓ ints❗️
        🍉

        💭Long Array
        🙅↪️ id 🙌 11 🍇
            🔢queue❗️ ➡️ length
            🆕🍨🐚💯🍆🐸❗️ ➡️ 🖍🆕longs
            
            🔂 i 🆕⏩⏩ 0 length❗️ 🍇
                🐻longs 0.0❗️
                🔂 j 🆕⏩⏩ 0 8❗️ 🍇
                    🎈queue❗️
                🍉
            🍉

            ↩️🆕😩🔸🍨🔸💯🆕 📛tag❓ longs❗️
        🍉

        ↩️🆕😩🔸🏁🆕❗️
    🍉

    🐇❗️📛t🔢➡️🔡 🍇
        🍨 🔤TAG_End🔤
           🔤TAG_Byte🔤
           🔤TAG_Short🔤
           🔤TAG_Int🔤
           🔤TAG_Long🔤
           🔤TAG_Float🔤
           🔤TAG_Double🔤
           🔤TAG_Byte_Array🔤
           🔤TAG_String🔤
           🔤TAG_List🔤
           🔤TAG_Compound🔤
           🔤TAG_Int_Array🔤
           🔤TAG_Long_Array🔤
        🍆 ➡️ types
        ↪ 🐔types❗️ ▶🙌 t 🍇
            ↩🐽types t❗️
        🍉
        ↩🔤Unknown🔤
    🍉

    🐇 ❗️ 🎠 x 🔡 i 🔢 ➡️ 🔡 🍇
        🆕🔠🆕❗️ ➡️ builder
        🔂 _ 🆕⏩⏩ 0 i✖2❗️ 🍇
            🐻builder x❗️
        🍉
        ↩️🔡builder❗️
    🍉
🍉